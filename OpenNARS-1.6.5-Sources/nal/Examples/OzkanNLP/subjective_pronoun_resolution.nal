******EXAMPLE 1: (Pronoun Resolution)
//John, Mary, cats, meat and water are instances of nouns:
<{"John", "Mary", "cats", "meat", "water"} --> nouns>.
//He, she, it and they are instances of pronouns:
<{"he", "she", "it", "they"} --> pronouns>.
//Pronouns and nouns are similar to some extent:
<pronouns <-> nouns>. %0.8;0.9%
//John eats meat:
<(*,"John","meat") --> eat>.
//he eats meat:
<(*,"he","meat") --> eat>.
//who is he?
<"he" <-> ?who>?

//the relation between John and meat is similar to the relation between he and meat:
 OUT: <(*,"John","meat") <-> (*,"he","meat")>. %0.8;0.45%
//he is John: (answer to the question)
 OUT: <"he" <-> "John">.


****EXAMPLE 2:  (Pronoun Resolution + Learning Gender)
//John, Mary, cats, meat and water are instances of nouns:
<{"John", "Mary", "cats", "meat", "water"} --> nouns>.
//He, she, it and they are instances of pronouns:
<{"he", "she", "it", "they"} --> pronouns>.
//Pronouns and nouns are similar to some extent:
<pronouns <-> nouns>. %0.8, 0.9%
//Mary is female:
<"Mary" --> [female]>.
//If someone is thirsty, that person drinks water:
<<$x --> [thirsty]> ==> <(*,$x,"water") -->  drink>>.
//Mary is thirsty:
<"Mary" --> [thirsty]>.
//She drinks water.
<(*,"she","water") --> drink>.
//Who is she?
<?who <-> ?she>?

//she is thirsty
 OUT: <she" --> [thirsty]. %0.8;0.81%
//she is Mary (answer to the question)
 OUT: <"she" <-> "Mary">. %0.8;0.42%
//she is female
 OUT: <"she" --> [female]>. %0.8;0.38%

******EXAMPLE 3: (Pronoun Resolution + Learning Plurality)
//John, Mary, cats, meat and water are instances of nouns:
<{"John", "Mary", "cats", "meat", "water"} --> nouns>.
//He, she, it and they are instances of pronouns:
<{"he", "she", "it", "they"} --> pronouns>.
//Pronouns and nouns are similar to some extent:
<pronouns <-> nouns>. %0.8;0.9%.
//John eats meat:
<(*,"John","meat") --> eat>.
//Cats are plural:
<"cats" --> [plural]>.
//Cats are hungry:
<"cats" --> [hungry]>.
//If someone is hungry, that person eats meat.
<<$x --> [hungry]> ==> <(*,$x,"meat") --> eat>>.
//They eat meat:
<(*,"they","meat") --> eat>.
//who are they?
<?who <-> "they">?

//they are hungry:
 OUT: <"they" --> [hungry]>. %0.8;0.81%
//they are cats (answer to the question)
 OUT: <"they" <-> "cats">. %0.8;0.42%
//they are plural
 OUT: <"they" --> [plural]>. %0.8;0.36%
